#include "protheus.ch" 
/*////////////Nota//////////////////
Cada vez que se agregue una constante
hay que agregar el ejemplo en la funcion List_box()
//////////////////////////////////*/
#DEFINE CAMP1 1 //Cliente 		 
#DEFINE CAMP2 2 //Tienda 		
#DEFINE CAMP3 3 //Moneda 		
#DEFINE CAMP4 4 //LC
#DEFINE CAMP5 5 //Vencimiento

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณNOVO2     บAutor  ณMicrosiga           บFecha ณ  01/10/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ  Cardeza Fernando                                          บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Modificaci๓n de listas de precios masiva                   บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/     
*-------------------------*
User Function MODSA1()   
*-------------------------*

Local aExcel	:={}
Local aCpo 		:={}
Local xAutoItem	:={}
Local aAutoErro 
Local aCabec	:={}  
Local aDados	:={}
Local aImport	:={}
Local lError	:=.F.

U_ListBoxIte() //Lista condiciones necesarias para la ejecusion del programa
U_List_Box() //Lista browser con ejemplo de la estructura que debe tener el excel 

If !(lOk := MsgYesNo("ฟDesea continuar con el proceso?","Advertencia") )
MsgInfo("Proceso cancelado " )
Return
Endif

aExcel:=U_CargaXLS() //genera el array del excel y lo vuelca en la matriz aExcel
For nI := 1 to Len(aExcel) 
	If nI == 1 // cabecera con los campos a ser migrados
		aCabec:=aClone(aExcel[nI])
	Else                          
		aDados:=aClone(aExcel[nI])
		If Len(aCabec) <> Len(aDados)
			If Len(aCabec) > Len(aDados)
				MsgAlert('Existen mแs campos definidos que datos. Ver lํnea: '+AllTrim(str(nI)),'Atenci๓n')
			Else
				MsgAlert('Existen mแs datos que campos definidos. Ver lํnea: '+AllTrim(str(nI)),,'Atenci๓n')
			Endif
			lError := .T.
		Else
			AAdd(aImport,aClone(aDados))
		Endif
	Endif
Next nI     

If !lError
	Processa({|| ModSA1(aCabec,aImport)},"Espere, Actualizando lista... Puede demorar")
Endif

Return

// 
// ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
// ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
// ฑฑบPrograma  ณ Grava    บ Autor ณ Ivaldo Junior      บ Data ณ  27/02/09   บฑฑ
// ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
// ฑฑบDesc.     ณExecuta a importa็ใo dos registros.                         บฑฑ
// ฑฑบ          ณ                                                            บฑฑ
// ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
// ฑฑบUso       ณ AP                                                         บฑฑ
// ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
// ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
// ฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿

Static Function ModSA1(aCabec,aImport)

Local nX 		:= 0
Local nCpo      := 0                            
Local cCod      := ""
Local nPosCod   := 0
Local nPosLoj   := 0
Local nCantErr  := 0
Local nCantErr1 := 0
Local lsalir    := .F.
Local aNoCpos   := {"A1_FILIAL"}
Local cDirLog   := "\log\"
Local cSubdir   := "clientes\"

Private lMsErroAuto 	:= .F.
Private aVector       	:= {}

ProcRegua(Len(aImport))

nPosCod := aScan( aCabec,{|x| allTrim(x) == "A1_COD" })
nPosLoj := aScan( aCabec,{|x| allTrim(x) == "A1_LOJA" })
For nX:=1 To Len(aImport)
	IncProc("Importaci๓n: Insertando registro "+AllTrim( Str(nX) )+" de "+AllTrim( Str(Len(aImport)) ))
	
	aVector  := {}
 	DbSelectArea("SA1")
 	DbSetOrder(1)

	If nPosCod+nPosLoj > 0 .And. DbSeek( xFilial("SA1")+aImport[nX][nPosCod]+aImport[nX][nPosLoj])
	
		For nCpo := 1 To Len(aCabec)
				Aadd(aVector,{Alltrim(aCabec[nCpo]),valorCpo(aCabec[nCpo],aImport[nX][nCpo]),Nil})
		Next nCpo
		
		MSExecAuto({|x,y| mata030(x,y)},aVector,4)   
						
		If lMsErroAuto
			ProcLogAtu("ERRO",'Atenci๓n',"Error en la modificacion de Clientes")
			If !ExistDir(cDirLog)		
				MakeDir(cDirLog)
			EndIf
			If !ExistDir(cDirLog+cSubdir)		
				MakeDir(cDirLog+cSubdir)
			EndIf
			MOSTRAERRO(cDirLog+cSubdir)
			lMsErroAuto := .F.
			DisarmTransaction()
			nCantErr += 1
		EndIf	
	Else                
	nCantErr1 += 1
	EndIf	
	
	If lsalir
		Exit
	Endif
Next

If nCantErr > 0 .or. nCantErr1 > 0
	If nCantErr > 0
		MsgAlert("Fin del Proceso. Registros con errores: "+str(nCantErr),'Atenci๓n')
    endif
    If nCantErr1 > 0
    	MsgAlert("Fin del proceso. Registros no encontrados: " + str(nCantErr1),'Atenci๓n')
    endif
else
MsgAlert('Proceso finalizado con ้xito','Atenci๓n')
Endif

//End Transaction

Return
       

/*/ 
+------------------------------------------------------------------------- 
| Funci๓n | LIST_BOX   | Autor | Cardeza Fernando        |Fecha |        |  
+------------------------------------------------------------------------- 
| Descripci๓n | Programa que muestra la utilizaci๓n de un LISTBOX() con |
|  
| grid.        															| 
+------------------------------------------------------------------------- 
| Uso  | Ventana para muestra de estructura del excel a importar
+------------------------------------------------------------------------- 
/*/ 
#include "protheus.ch"

User Function List_Box() 
Local aVetor := {} 
Local oDlg 
Local oLbx 
Local cTitulo := "Excel para modificaci๓n" 


AADD( aVetor, { "Carแcter de 6", "Carแcter de 2" , "N๚merico" , "N๚merico" , "Fecha" } ) 
AADD( aVetor, { "000332", "01" , "2" , "1000", "01/01/2015" } )
// Monta la pantalla para que el usuario visualice consulta. 

DEFINE MSDIALOG oDlg TITLE cTitulo FROM 0,0 TO 240,500 PIXEL

@ 10,10 LISTBOX oLbx FIELDS HEADER "A1_COD", "A1_LOJA" ,"A1_MOEDALC" ,"A1_LC" , "A1_VENCLC" SIZE 240,95 OF oDlg	PIXEL
	oLbx:SetArray( aVetor ) 
	oLbx:bLine := {|| {aVetor[oLbx:nAt,CAMP1],;
					   aVetor[oLbx:nAt,CAMP2],; 
					   aVetor[oLbx:nAt,CAMP3],;
					   aVetor[oLbx:nAt,CAMP4],;
					   aVetor[oLbx:nAt,CAMP5]}} 
DEFINE SBUTTON FROM 107,213 TYPE  1 ACTION oDlg:End() ENABLE OF oDlg 
ACTIVATE MSDIALOG oDlg CENTER 
Return                                      

/*
+-------------------------------------------------------------------------- 
| Funci๓n | LISTBOXITE    | Autor | Cardeza Fernando    | Fecha |         |
|+------------------------------------------------------------------------- 
| Descripci๓n                                                             |
| Programa que muestra la utilizaci๓n de LISTBOX() como lista simple.     |   
|+------------------------------------------------------------------------- 
| Uso  | Muestra las condiciones necesarias para la ejecusion de la rutina|
|+------------------------------------------------------------------------- 
*/ 
User Function ListBoxIte() 
Local aVetor   := {} 
Local oDlg     := Nil 
Local oLbx     := Nil 
Local cTitulo  := "Leer antes de comenzar..." 
Local nChave   := 0 
Local cChave   := "" 
AADD( aVetor, "El siguiente programa ejecuta una rutina automแtica para la modificaci๓n masiva de precios") 
AADD( aVetor, "------------------------------------------------------------------------------------------")
AADD( aVetor, "Enumeraci๓n de condiciones necesarias para el correcto funcionamiento de la rutina")
AADD( aVetor, "1 - El c๓digo y tienda deben existir en el maestro de clientes")
AADD( aVetor, "2 - El usuario en su pc local debe tener instalado el Ms Office") 
AADD( aVetor, "3 - El archivo seleccionado debe tener extensi๓n *.xls (Libro de Excel 97-2003)") 
AADD( aVetor, "4 - El archivo a importar debe tener la estructura del siguiente browser") 
//+-----------------------------------------------+
//| Monta la pantalla para que el usuario visualice consulta |        
//+-----------------------------------------------+

DEFINE MSDIALOG oDlg TITLE cTitulo FROM 0,0 TO 240,500 PIXEL 
@ 10,10 LISTBOX oLbx VAR nChave ITEMS aVetor SIZE 230,95 OF oDlg PIXEL 
oLbx:bChange := {|| cChave := SubStr(aVetor[nChave],1,2) } 
DEFINE SBUTTON FROM 107,213 TYPE  1 ACTION oDlg:End() ENABLE OF oDlg 
ACTIVATE MSDIALOG oDlg CENTER 
Return                                                      

// 
// ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
// ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
// ฑฑบPrograma  ณ valorCpo บ Autor ณ Fernando Cardeza   บ Data ณ  27/02/13   บฑฑ
// ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
// ฑฑบDesc.     ณ Retorna el valor formateado segun el tipo de dato del cpo  บฑฑ
// ฑฑบ          ณ                                                            บฑฑ
// ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
// ฑฑบUso       ณ AP                                                         บฑฑ
// ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
// ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
// ฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿

Static Function valorCpo(cCampo,cValor)	

Local aArea    := GetArea()
Local aAreaSX3 := SX3->(GetArea())
Local xValor

dbSelectArea("SX3")
dbSetOrder(2)
If dbSeek(allTrim(cCampo))
	If X3_TIPO $ "C/M"
		xValor := cValor
	Elseif X3_TIPO == "N"
		xValor := Val(cValor)
	Elseif X3_TIPO == "D"
		xValor := CtoD(cValor)
	Endif
EndIf

RestArea(aAreaSX3)
RestArea(aArea)

Return xValor